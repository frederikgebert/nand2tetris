// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/Memory.hdl
/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load=1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid and reads 0. Access to any address
 * in the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the Screen
 * and Keyboard chip specifications given in the lectures and the book.
 */
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    Not(in=address[14], out=ram);
    And(a=load, b=ram, out=ramload);
    RAM16K(in=in, load=ramload, address=address[0..13], out=ramout);

    Not(in=address[13], out=t);
    And(a=t, b=address[14], out=screen);
    And(a=load, b=screen, out=screenload);
    Screen(in=in, load=screenload, address=address[0..12], out=screenout);
    
    Or8Way(in=address[0..7], out=x);
    Or8Way(in[0]=x, in[1..5]=address[8..12], in[6..7]=false, out=y);
    Not(in=y, out=z);
    And(a=address[13], b=address[14], out=zz);
    And(a=zz, b=z, out=kbd);
    Keyboard(out=kbdout);

    Mux16(a=false, b=ramout, sel=ram, out=a);
    Mux16(a=a, b=screenout, sel=screen, out=b);
    Mux16(a=b, b=kbdout, sel=kbd, out=out);
}